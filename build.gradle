// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id "com.diffplug.spotless" version "5.3.0"
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        google()
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    if (it.name != 'app') {
        spotless {
            java {
                target fileTree('.') {
                    include '**/*.java'
                }
                googleJavaFormat('1.7').aosp()
                licenseHeaderFile project.rootProject.file('spotless/copyright.java')
            }
        }
    } else {
        spotless {
            java {
                target '**/*.java'
                googleJavaFormat('1.7').aosp()
            }
        }
    }
}


ext {
    android = [
            compileSdkVersion: 30,
            buildToolsVersion: "30.0.3",
            minSdkVersion    : 14,
            targetSdkVersion : 30,
            versionCode      : 15,
            versionName      : "0.2.1"
    ]
    bintray = [
            siteUrl       : 'https://github.com/dkzwm/FormatEditText',
            gitUrl        : 'https://github.com/dkzwm/FormatEditText.git',
            groupId       : "com.github.dkzwm",
            libVersion    : android.versionName,
            developerId   : "dkzwm",
            developerName : "Zhang Weiming",
            developerEmail: "aidkzwm@gmail.com",
            licenseName   : "MIT License",
            licenseUrl    : "https://opensource.org/licenses/MIT",
    ]
    allLicenses = ['MIT']
}
